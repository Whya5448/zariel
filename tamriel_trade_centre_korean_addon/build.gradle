plugins {
    id 'org.beryx.jlink'
    id 'org.jetbrains.kotlin.plugin.spring'
    id 'org.springframework.boot'
}

dependencies { compile project(':library') }

def sImageName = 'TamrielTradeCenter_Korean_Addon'
def sModuleName = 'org.metalscraps.eso.lang.tamriel.trade.centre.korean.addon'
def sMainClass = 'org.metalscraps.eso.lang.tamriel.trade.centre.korean.addon.TamrielTradeCentreTableWatcher'
mainClassName = "${sModuleName}/${sMainClass}"
jar.manifest { attributes 'Implementation-Title': sModuleName, 'Main-Class': sMainClass }

jlink {
    mergedModuleName = "${sModuleName}.merged"
    imageName = sImageName
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageZip = file("$buildDir/${sImageName}-${version}.zip")
    launcher {
        name = 'launch'
        jvmArgs = ['-Dlogback.configurationFile=./logback.xml']
    }
    mergedModule {
        requires 'java.management'
        requires 'java.naming'
        requires 'java.logging'
        requires 'java.sql'
        requires 'java.xml'
        requires 'java.desktop'
        requires 'java.net.http'
        requires 'java.security.jgss'
        requires 'jdk.unsupported'
        uses 'java.nio.file.spi.FileSystemProvider'
        provides 'com.fasterxml.jackson.core.JsonFactory' with 'com.fasterxml.jackson.core.JsonFactory'
        provides 'com.fasterxml.jackson.core.ObjectCodec' with 'com.fasterxml.jackson.databind.ObjectMapper'
    }
}

tasks.jlink.doLast {
    copy {
        from('src/main/resources')
        into("$buildDir/${sImageName}/bin")
    }
}