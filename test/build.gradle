plugins {
    id 'org.beryx.jlink'
    id 'org.jetbrains.kotlin.plugin.spring'
    id 'org.springframework.boot'
}
apply plugin: 'io.spring.dependency-management'
dependencies {
    configurations.all {
        exclude group: "commons-logging", module: "commons-logging"
    }

    compile project(':library')
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.springframework.boot:spring-boot-starter:2.1.4.RELEASE'
}

def sImageName = 'dc-eso-test'
def sModuleName = 'org.metalscraps.eso.lang.test'
def sMergedModuleName = "${sModuleName}.merged.module"
def sMainClass = 'org.metalscraps.eso.lang.test.TestApplication'
def launcherName = 'launch'
def platformName = 'linux-x64'
mainClassName = "${sModuleName}/${sMainClass}"
springBoot { mainClassName = sMainClass }
jar {
    manifest { attributes 'Implementation-Title': sModuleName, 'Main-Class': sMainClass }
    enabled = true
}
jlink {
    mergedModuleName = "${sMergedModuleName}"
    imageName = sImageName
    //options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    options = []
    //imageZip = file("$buildDir/${sImageName}-${version}.zip") // 작동이 되다 말다 함. targetPlatform 관련한 문제가 있는듯.
    targetPlatform(platformName, 'C:/Program Files/Zulu/zulu12.2.3-ca-jdk12.0.1-linux_x64')
    launcher {
        name = launcherName
        jvmArgs = ['-Xmx768M', '-Dlogback.configurationFile=./logback.xml',
                   "--add-reads ${sModuleName}.merged.module=${sModuleName}",
                   "-cp", "config/"]
    }
    mergedModule {
        requires 'java.management'
        requires 'java.naming'
        requires 'java.logging'
        requires 'jdk.httpserver'
        requires 'java.scripting'
        requires 'java.sql'
        requires 'java.prefs'
        requires 'java.rmi'
        requires 'java.xml'
        requires 'java.desktop'
        requires 'java.instrument'
        requires 'jdk.unsupported'
        uses 'kotlin.reflect.jvm.internal.impl.util.ModuleVisibilityHelper'
        uses 'kotlin.reflect.jvm.internal.impl.builtins.BuiltInsLoader'
        uses 'kotlin.reflect.jvm.internal.impl.resolve.ExternalOverridabilityCondition'
        uses 'java.nio.file.spi.FileSystemProvider'
        provides 'com.fasterxml.jackson.core.JsonFactory' with 'com.fasterxml.jackson.core.JsonFactory'
        provides 'org.apache.commons.logging.LogFactory' with 'org.apache.commons.logging.LogFactoryService'
        provides 'kotlin.reflect.jvm.internal.impl.resolve.ExternalOverridabilityCondition' with 'kotlin.reflect.jvm.internal.impl.load.java.ErasedOverridabilityCondition',
                'kotlin.reflect.jvm.internal.impl.load.java.FieldOverridabilityCondition',
                'kotlin.reflect.jvm.internal.impl.load.java.JavaIncompatibilityRulesOverridabilityCondition'
        provides 'kotlin.reflect.jvm.internal.impl.builtins.BuiltInsLoader' with 'kotlin.reflect.jvm.internal.impl.serialization.deserialization.builtins.BuiltInsLoaderImpl'
        provides 'com.fasterxml.jackson.core.ObjectCodec' with 'com.fasterxml.jackson.databind.ObjectMapper'
    }
}
tasks.jlink.doLast {
    copy {
        from('src/main/resources')
        into("$buildDir/${sImageName}/${launcherName}-${platformName}/bin/config")
    }

    copy {
        from "$buildDir/classes/kotlin/main/org/metalscraps/eso/lang/test"
        into "$buildDir/${sImageName}/${launcherName}-${platformName}/bin/config/org/metalscraps/eso/lang/test/"
    }

}
bootRun {
    //suspend => 접속전까지 멈출지
    //jvmArgs=["-Xmx2G", "-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005"]
    jvmArgs=["-Xmx768M", "-XX:+PrintCommandLineFlags", "-verbose:gc"]
}