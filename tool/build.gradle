plugins {
    id 'org.beryx.jlink' version '2.4.4'
    id "org.javamodularity.moduleplugin" version "1.4.0"
    id 'org.jetbrains.kotlin.jvm' version '1.3.20'
}

dependencies {
    compile project(':library')
    implementation "commons-io:commons-io:${commonsio}"
    implementation 'org.apache.commons:commons-lang3:3.7'
    implementation 'org.apache.commons:commons-text:1.2'
    implementation 'com.google.cloud:google-cloud-translate:1.14.0'
    implementation "com.fasterxml.jackson.core:jackson-databind:${jackson}"
    implementation 'org.jsoup:jsoup:1.11.2'
}

def sImageName = 'dc-eso-tool'
def sModuleName = 'org.metalscraps.eso.lang.tool'
def sMainClass = 'org.metalscraps.eso.lang.tool.ToolMain'
mainClassName = "${sModuleName}/${sMainClass}"
jar.manifest { attributes 'Implementation-Title': sModuleName, 'Main-Class': sMainClass }

run {
    args = ["-opt=help"]
    // 폴더 설정 참조
    // args = ["-opt=12412", "-base=12", "-po=C:\\Users\\Whya5\\Documents\\Elder Scrolls Online\\EsoKR\\PO_0116"]
}

jlink {
    mergedModuleName = "${sModuleName}.merged"
    imageName = sImageName
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageZip = file("$buildDir/${sImageName}-${version}.zip")
    launcher {
        name = 'launch'
        jvmArgs = ['-Xmx2G', '-Dlogback.configurationFile=./logback.xml']
    }
    mergedModule {
        requires 'java.management'
        requires 'java.naming'
        requires 'java.logging'
        requires 'java.sql'
        requires 'java.xml'
        requires 'java.desktop'
        requires 'java.compiler'
        requires 'jdk.unsupported'
        uses 'org.threeten.bp.zone.ZoneRulesProvider'
        uses 'org.threeten.bp.chrono.Chronology'
        uses 'java.nio.file.spi.FileSystemProvider'
        uses 'com.google.auto.value.processor.AutoValueProcessor'
        provides 'com.fasterxml.jackson.core.JsonFactory' with 'com.fasterxml.jackson.core.JsonFactory'
        provides 'javax.annotation.processing.Processor' with 'com.google.auto.value.processor.AutoAnnotationProcessor',
                'com.google.auto.value.processor.AutoValueBuilderProcessor',
                'com.google.auto.value.processor.AutoValueProcessor'
        provides 'org.threeten.bp.zone.ZoneRulesProvider' with 'org.threeten.bp.zone.TzdbZoneRulesProvider'
        provides 'com.fasterxml.jackson.core.ObjectCodec' with 'com.fasterxml.jackson.databind.ObjectMapper'
    }
}

tasks.jlink.doLast {
    copy {
        from('src/main/resources')
        into("$buildDir/${sImageName}/bin")
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

