plugins {
    id 'org.beryx.jlink' version '2.10.1'
    id "org.javamodularity.moduleplugin" version "1.5.0"
    id 'org.jetbrains.kotlin.jvm' version "1.3.30"
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.30'
    id 'org.hidetake.ssh' version '2.10.1'
    id 'org.springframework.boot' version '2.1.4.RELEASE'
}
apply plugin: 'io.spring.dependency-management'
dependencies {
    configurations.all {
        exclude group: "commons-logging", module: "commons-logging"
        exclude group: "com.google.code.findbugs", module: "jsr305"
    }

    compile project(':library')
    implementation 'com.google.apis:google-api-services-compute:v1-rev199-1.25.0'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.springframework.boot:spring-boot-starter:2.1.4.RELEASE'
}

def sImageName = 'dc-eso-server'
def sModuleName = 'org.metalscraps.eso.lang.server'
def sMergedModuleName = "${sModuleName}.merged.module"
def sMainClass = 'org.metalscraps.eso.lang.server.ServerApplication'
def launcherName = 'launch'
def platformName = 'linux-x64'
mainClassName = "${sModuleName}/${sMainClass}"
springBoot { mainClassName = sMainClass }
jar {
    manifest { attributes 'Implementation-Title': sModuleName, 'Main-Class': sMainClass }
    enabled = true
}
jlink {
    mergedModuleName = "${sMergedModuleName}"
    imageName = sImageName
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    //options = []
    //imageZip = file("$buildDir/${sImageName}-${version}.zip") // 작동이 되다 말다 함. targetPlatform 관련한 문제가 있는듯.
    targetPlatform(platformName, 'C:/Program Files/Zulu/zulu11.29.3-ca-jdk11.0.2-linux_x64')
    launcher {
        name = launcherName
        jvmArgs = ['-Xmx2G', '-Dlogback.configurationFile=./logback.xml',
                   "--add-reads ${sModuleName}.merged=${sModuleName}",
                   "--add-reads ${sModuleName}.merged.module=${sModuleName}",
                   "-cp", "config/"]
    }
    mergedModule {
        requires 'java.logging'
        requires 'jdk.httpserver'
        requires 'java.sql'
        requires 'java.prefs'
        requires 'java.rmi'
        requires 'java.xml'
        requires 'java.desktop'
        requires 'java.security.jgss'
        requires 'jdk.unsupported'
        requires 'java.management'
        requires 'java.naming'
        requires 'java.scripting'
        requires 'java.instrument'
        uses 'kotlin.reflect.jvm.internal.impl.util.ModuleVisibilityHelper'
        uses 'kotlin.reflect.jvm.internal.impl.builtins.BuiltInsLoader'
        uses 'kotlin.reflect.jvm.internal.impl.resolve.ExternalOverridabilityCondition'
        uses 'java.nio.file.spi.FileSystemProvider'
        provides 'com.fasterxml.jackson.core.JsonFactory' with 'com.fasterxml.jackson.core.JsonFactory'
        provides 'org.apache.commons.logging.LogFactory' with 'org.apache.commons.logging.LogFactoryService'
        provides 'kotlin.reflect.jvm.internal.impl.resolve.ExternalOverridabilityCondition' with 'kotlin.reflect.jvm.internal.impl.load.java.ErasedOverridabilityCondition',
                'kotlin.reflect.jvm.internal.impl.load.java.FieldOverridabilityCondition',
                'kotlin.reflect.jvm.internal.impl.load.java.JavaIncompatibilityRulesOverridabilityCondition'
        provides 'kotlin.reflect.jvm.internal.impl.builtins.BuiltInsLoader' with 'kotlin.reflect.jvm.internal.impl.serialization.deserialization.builtins.BuiltInsLoaderImpl'
        provides 'com.fasterxml.jackson.core.ObjectCodec' with 'com.fasterxml.jackson.databind.ObjectMapper'
    }
}
tasks.jlink.doLast {
    copy {
        from('src/main/resources')
        into("$buildDir/${sImageName}/${launcherName}-${platformName}/bin/config")
    }

    copy {
        from "$buildDir/classes/kotlin/main/org/metalscraps/eso/lang/server"
        into "$buildDir/${sImageName}/${launcherName}-${platformName}/bin/config/org/metalscraps/eso/lang/server/"
    }

}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

remotes {
    server {
        host = sHost
        user = sUser
        port = sPort as int
        identity = file('id_rsa')
        passphrase = sPassphrase
    }
}


task zip(type: Zip, dependsOn : 'jlink') {
    archiveFileName = "${launcherName}-${platformName}.zip"
    destinationDirectory = file("$buildDir")
    from "$buildDir/${sImageName}/${launcherName}-${platformName}"
}


task deploy(dependsOn : 'zip') {
    doLast {
        ssh.run {
            settings { knownHosts = file('known_hosts') }
            session(remotes.server) {
                put from: "$buildDir/${launcherName}-${platformName}.zip", into: sPath
                executeSudo sExecute
            }
        }
    }
}